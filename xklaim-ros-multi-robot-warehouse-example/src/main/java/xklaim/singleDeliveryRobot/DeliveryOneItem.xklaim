package xklaim.singleDeliveryRobot

import klava.Locality

import static xklaim.GlobalConstants.*
import static xklaim.singleDeliveryRobot.DeliveryRobotConstants.*

proc DeliveryOneItem(String robotId,String sector,Locality Arm) {
	// wait for a call by the arm robot
	in(ITEM_READY_FOR_DELIVERY,sector)@Arm

	// move to the arm position (notably, the arm robot has a fixed, known position)  
	val x = -0.21
	val y = 0.31
	eval(new MoveTo(robotId, x, y))@self
	in(MOVE_TO_COMPLETED)@self

	// notify the arm robot that it is arrived and is ready to receive the item
	out(DELIVERY_ROBOT_ARRIVED)@Arm

	// wait for the notification that the gripper is opened 
	in(GRIPPER_OPENED,var String itemId,var String itemType)@Arm

	// activate the pressure sensor for detecting the presence of the item in the bowl and wait for the item
	eval(new WaitForItem(robotId))@self
	in(ITEM_LOADED)@self

	// retrieve from the local knowledge the delivery destination coordinates
	read(TYPE_2_DESTINATION,itemType,var Double x2,var Double y2)@self

	// move to the delivery destination position
	eval(new MoveTo(robotId, x2, y2))@self
	in(MOVE_TO_COMPLETED)@self

	// notify that the item with identifier itemId has been delivered
	out(ITEM_DELIVERED,itemId,x2,y2)@self

	// set the robot as available for a new delivery
	out(AVAILABLE_FOR_DELIVERY)@self
}
